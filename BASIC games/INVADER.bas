10 GOSUB860:GOSUB810:CLS:CLEAR5000:DIML(511),Y(542),T(542),K(255),L$,B$,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,AK,BC,N1,LV,SC:GOTO500
20 RETURN
30 FORU=I TO B:RESET(Y,K):K=K-I:ONPOINT(Y,K)GOTO60,60,60,40,40,40,40,270:SET(Y,K,I):NEXT:RESET(Y,K):RETURN
40 H=INT(K/W)*G+INT(Y/W):PRINT@H-I,CHR$(N2)CHR$(N2+RND(N7))CHR$(N2+RND(N7));:AK=AK+I:IFAK>N1 THENU=B:NEXT:L=E:RETURN
50 VP=VARPTR(L$):FORZ=-I TOI:POKE(PEEK(VP+W)*V)+PEEK(VP+R)+(H-L)+Z,N2:NEXT:SET(Y,K,I):U=B:K=O:NEXT:RETURN
60 RESET(Y,K):U=B:K=O:Y=O:NEXT:RESET(Y,K):RETURN
70 RESET(C,M):M=M+W:ONPOINT(C,M)GOTO240,150,150:SET(C,M,I):RETURN
80 T=R:NEXT:OND+I GOTO90,90:S=L:L=E:NEXT:D=I:F=O:E=N9:X=S1:GOTO100
90 S=L:L=E:NEXT:D=-I:F=N1:E=N1:X=R:MS=MS+W:PRINT@MS,M$;:IFMS>N8 THENPRINT@MS,LEFT$(B$,5);:MS=O
100 BC=BC+I:IFBC>(G-AK)/W AND S<N6 THENBC=O:PRINT@S,LEFT$(B$,L(S));:S=S+G
110 FORL=S TO E STEPD:PRINT@L,LEFT$(L$,L(L));:ONPOINT(P,N)GOSUB20,20,20,290,290,290,290:ONPOINT(P+W,N)GOSUB20,20,20,290,290,290,290
120 PRINT@Q,P$;:ON M GOSUB220,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,70,210,210,210
130 J=L+F:FORT=O TO T(J):ONPOINT(X,Y(J)+V(T))GOTO180,180,180,80,80,80,80:NEXT
140 ON1-((PEEK(343)ANDPEEK(344)ANDPEEK(345))=255)GOSUB190:ONK GOSUB30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30,30:NEXT:GOTO300
150 SET(C,M,I):GOSUB250:RETURN
160 ON(P>B)+I GOTO20:P=P-W:Q=Q-I:RETURN
170 ON(P<N4)+I GOTO20:P=P+W:Q=Q+I:RETURN
180 T=R:NEXT:GOTO140
190 ONK(PEEK(135))GOSUB160,170,200:RETURN
200 ON K GOTO20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20,20:K=N5:Y=P:RETURN
210 RESET(C,M):M=I
220 ONRND(R)GOTO20,20:FORZ=N3 TO R STEP-W:ONPOINT(P,Z)GOSUB20,20,20,230,230,230,230,230:NEXT:RETURN
230 M=Z:Z=R:C=P+I:RETURN
240 RESET(C,M):RESET(C,M-I):M=I:RETURN
250 SOUND1,3:NP=NP-I:POKE1535,48+NP:IFNP<I THENS=L:L=E:K=O:M=O:X=O:V(O)=Y(L+F)*-I:PRINT@Q+I,CHR$(160+RND(N7))CHR$(160+RND(N7));
260 RETURN
270 H=INT(K/W)*G+INT(Y/W):PRINT@H-I,CHR$(240+RND(14))CHR$(240+RND(14))CHR$(240+RND(14));
280 SOUND200,1:SOUND155,2:SOUND230,1:RESET(Y,K):U=B:K=O:NEXT:RESET(Y,K):PRINT@MS,LEFT$(B$,6);:MS=0:SC=SC+25:RETURN
290 NP=I:GOTO250
300 IFNP>O AND AK=32 THEN390
310 PRINT@MS,LEFT$(B$,6);:IFS<448 THEN FORL=S TO 479 STEP32:PRINT@L-N1,LEFT$(B$,L(L-N1));:PRINT@L,LEFT$(L$,L(L));:SOUND1,2:NEXT
320 PRINT@0, "YOU ARE DEAD.      ";:SC=SC+(AK*LV):PRINT"SCORE=";SC:IFSC>HS THEN HS=SC
330 PRINT@32,"PLAY AGAIN (Y/N)?  ";:PRINT"HIGH=";HS
340 IFAK<32 THEN PRINT@196,"THE ALIENS HAVE LANDED!";:SOUND100,15
350 I$=INKEY$:IFI$=""THEN350
360 IFI$="Y" THEN 680
370 IFI$="N"THENGOSUB910:GOSUB840:END
380 GOTO350
390 PRINT@0,"ANOTHER WAVE IS COMING!"
400 SOUND100,3:SOUND50,3:SOUND100,3:SOUND50,5:SOUND50,5:SOUND100,3:SOUND50,3
410 SC=SC+(32*LV):PRINT@266,"SCORE=";SC;:FORT=1TO2000:NEXT
420 IFNP=SS THEN PRINT@198,"+100 SURVIVAL BONUS";:FORT=1 TO 2:FORZ=1 TO 24:READAA,BB:SOUNDAA,BB:NEXT:RESTORE:NEXT:SC=SC+100
430 XM=XM+I:IFXM=2 THEN GOSUB450
440 GOSUB700:GOTO110
450 XM=0:IFNP=3 THEN 490
460 PRINT@198,"*SHIELDS REPAIRED!*";
470 NP=NP+1
480 FORT=1 TO 10:POKE1535,48+NP+64:PRINT@Q,Q$;:FORZ=1 TO 200:NEXT:POKE1535,48+NP:PRINT@Q,P$;:FORZ=1 TO 200:NEXT:NEXT
490 RETURN
500 PRINT@231,"* SPACE INVADERS *";
510 PRINT@291,"FOR THE MC-10 BY JIM GERRIE";
520 PRINT@485,"PRESS ANY KEY TO BEGIN";
530 P$=INKEY$:R=RND(1000):IFP$="" THEN 530
540 CLS:PRINT"PLEASE WAIT..."
550 FORT=1TO32:B$=B$+CHR$(128):NEXT
560 O=0:I=1:W=2:R=3:B=4:G=32:N=29:V=256:N1=31
570 K(65)=1:K(83)=2:K(32)=3:K(8)=1:K(9)=2:A=17023
580 DIMN2,N3,N4,N5,N6,N7,N8,N9,MS,NS,XM,NP,SS,Q$,P$,I$,M$,V(3),VP,S1
590 P$=CHR$(128)+CHR$(135+32)+CHR$(139+32)+CHR$(128):Q$=CHR$(128)+CHR$(135+16)+CHR$(139+16)+CHR$(128)
600 N2=128:N3=27:N4=58:N5=28:N6=480:N7=14:N8=24:N9=479:S1=61:SH$=CHR$(135+32)+CHR$(139+32)
610 FORT=0TO256:L(T)=224:NEXT:FORT=257TO479:L(T)=L(T-1)-1:NEXT
620 FORT=0TO286:T(T)=3:Y(T)=INT((T/16)+.1):NEXT
630 FORT=287TO350:T(T)=2:Y(T)=INT((T/16)+.1):NEXT
640 FORT=351TO414:T(T)=1:Y(T)=INT((T/16)+.1):NEXT
650 FORT=415TO478:T(T)=0:Y(T)=INT((T/16)+.1):NEXT
660 FORT=479TO542:T(T)=0:Y(T)=28:NEXT
670 FORT=0TO3:V(T)=B*T:NEXT:M$=CHR$(128)+CHR$(128)+CHR$(241)+CHR$(255)+CHR$(251)
680 SC=0:LV=0:NS=0:NP=3:MS=0:XM=0:GOSUB700
690 GOTO110
700 CLS0:D=1:E=479:X=61:V(0)=0:NS=NS+1:LV=LV+1:IFLV>4 THEN LV=4
710 PRINT@480,NS;"    ";:FORT=1504TO1508:POKET,PEEK(T)-64:NEXT:FORT=1509TO1535:POKET,G:NEXT:POKE1535,48+NP
720 FORT=1TO3:PRINT@348+(9*T),CHR$(135);CHR$(143);CHR$(139);:PRINT@348+(9*T)+32,CHR$(143);CHR$(143);CHR$(143);:NEXT
730 P=31:Q=448+14:M=1:L=LV*32:S=L:AK=0:BC=0:F=0:H=0:K=0:U=0:Z=0
740 L$="":C=32:FORY=1TO4:C=C+16:FORT=1TO8:L$=L$+CHR$(128+C)+CHR$(133+C)+CHR$(141+C):NEXT:IFY=4 THEN L$=L$+LEFT$(B$,W):GOTO760
750 L$=L$+B$+LEFT$(B$,8)
760 NEXT:C=0:SS=NP:RETURN
770 DATA 89,1,125,1,147,1,176,1,147,1,125,1
780 DATA 89,1,125,1,147,1,89,1,147,1,125,1
790 DATA 89,1,125,1,147,1,176,1,147,1,125,1
800 DATA 89,1,125,1,147,1,89,1,147,1,125,1
810 IF PEEK(65535)=27 THEN POKE65497,0:GOTO830 :ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
820 IF A$="Y" THEN POKE65495,0 :ELSE IF A$<>"N" THEN810
830 CLS:RETURN
840 IF PEEK(65535)=27 THEN POKE65496,0 :ELSE POKE65494,0
850 RETURN
860 :' ENABLE DRAGON SPEEDKEY
870 IF PEEK(65535)<>180 THEN 890
880 IF PEEK(269)+PEEK(270)<>1 THEN POKE65283,52:POKE256,116:POKE257,1:POKE258,81:POKE259,126:POKE260,PEEK(269):POKE 261,PEEK(270):POKE269,1:POKE270,0:POKE65283,53
890 RETURN
900 :' DISABLE DRAGON SPEEDKEY
910 IF PEEK(65535)<>180 THEN 930
920 IF PEEK(269)+PEEK(270)=1 THEN POKE65283,52:POKE269,PEEK(260):POKE270,PEEK(261):POKE65283,53
930 RETURN